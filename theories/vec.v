(**************************************************************)
(*   Copyright Dominique Larchey-Wendling    [*]              *)
(*             Jean-FranÃ§ois Monin           [+]              *)
(*                                                            *)
(*           [*] Affiliation LORIA - CNRS                     *)
(*           [+] Affiliation VERIMAG - Univ. Grenoble Alpes   *)
(**************************************************************)
(*      This file is distributed under the terms of the       *)
(*        CeCILL v2.1 FREE SOFTWARE LICENSE AGREEMENT         *)
(**************************************************************)

From Coq Require Import Utf8.

From MuRec Require Import index.

Reserved Notation "v .[ i ]" (at level 1, format "v .[ i ]").
Reserved Notation "x 'âˆ·' v" (at level 60, right associativity, format "x  âˆ·  v").
Reserved Notation "âŸ¨ âŸ©" (at level 0, format "âŸ¨ âŸ©").

Section vec_basics.

  Variable X : Type.

  Inductive vec : nat â†’ Type :=
    | vec_nil  : vec 0
    | vec_cons {n} : X â†’ vec n â†’ vec (S n).

  Notation "âŸ¨ âŸ©" := vec_nil.
  Infix "âˆ·" := vec_cons.

  Inductive vec_shape_O : vec 0 â†’ Type :=
    | vec_shape_O_intro : vec_shape_O âŸ¨âŸ©.

  Inductive vec_shape_S n : vec (S n) â†’ Type :=
    | vec_shape_S_intro x v : vec_shape_S n (x âˆ· v).

  Definition vec_inv {n} v :
    match n return vec n -> Type with
    | 0   => vec_shape_O
    | S n => vec_shape_S n
    end v.
  Proof. destruct v; constructor. Qed.

  Section vec_O_inv.

    Let vec_O_inv_t {n} : vec n â†’ Type :=
      match n with
      | 0 => Î» v, âˆ€P, P âŸ¨âŸ© â†’ P v
      | _ => Î» _, Empty_set â†’ unit
      end.

    Variables (P : vec 0 â†’ Type)
              (HP : P âŸ¨âŸ©).

    Definition vec_O_inv v : P v :=
      match v return vec_O_inv_t v with
      | âŸ¨âŸ© => Î» P HP, HP
      | _  => Î» C, match C with end
      end P HP.

  End vec_O_inv.

  Section vec_S_inv.

    Let vec_S_inv_t {n} : vec n â†’ Type :=
      match n with
      | 0 => Î» _, Empty_set â†’ unit
      | _ => Î» v, âˆ€P, (âˆ€x w, P (x âˆ· w)) â†’ P v
      end.

    Variables (n : nat)
              (P : vec (S n) â†’ Type)
              (HP : âˆ€x v, P (x âˆ· v)).

    Definition vec_S_inv v : P v :=
      match v return vec_S_inv_t v with
      | âŸ¨âŸ©    => Î» C, match C with end
      | y âˆ· u => Î» P HP, HP y u
      end P HP.

  End vec_S_inv.

  Arguments vec_S_inv {_ _}.

  (* Beware that with that definition, vec_{head,tail} are not recognized
     as sub-terms of v. This could be needed in case of Fixpoints on
     inductive types nesting vec. The cause of this is the dependent pattern-matching
     returning vec_S_inv_t in vec_S_inv. The more standard definition of
     vec_head (resp. vec_tail) does not have this issue because the return type
     is eg is_succ n â†’ X which becomes non-dependent when (n := S _). *)

  Definition vec_head {n} : vec (S n) â†’ X := vec_S_inv (Î» x _,x).
  Definition vec_tail {n} : vec (S n) â†’ vec n := vec_S_inv (Î» _ v,v).

  Goal âˆ€n (v : vec (S n)), v = vec_head v âˆ· vec_tail v.
  Proof. intros n v; now destruct (vec_inv v). Qed.

  (* This definition is compatible with the recalg_rect Fixpoint, meaning
     that (vec_prj v i) is recognized as a sub-term of v in that Fixpoint *)

  Fixpoint vec_prj {n} (u : vec n) : idx n â†’ X :=
    match u in vec m return idx m â†’ X with
    | âŸ¨âŸ©    => Î» i, match idx_inv i with end
    | x âˆ· v => Î» i,
      match i in idx m return vec (pred m) â†’ X with
      | ð•†   => Î» _, x
      | ð•Š j => Î» v, v.[j]
      end v
    end
  where "v .[ i ]" := (vec_prj v i).

  Local Example vec_prj_conv_eq_0 n x (v : vec n) : (x âˆ· v).[ð•†] = x.
  Proof. reflexivity. Qed.

  Local Example vec_prj_conv_eq_S n x (v : vec n) i : (x âˆ· v).[ð•Š i] = v.[i].
  Proof. reflexivity. Qed.

  Fact vec_prj_ext {n} (v w : vec n) : (âˆ€i, v.[i] = w.[i]) â†’ v = w.
  Proof.
    revert w; induction v as [ | n x v IHv ]; intros w H.
    + now destruct (vec_inv w).
    + destruct (vec_inv w) as [y w]; f_equal.
      * apply (H ð•†).
      * apply IHv.
        intro; apply (H (ð•Š _)).
  Qed.

End vec_basics.

Arguments vec_nil {_}.
Arguments vec_cons {_ _}.
Arguments vec_inv {_ _}.
Arguments vec_prj {_ _}.
Arguments vec_head {_ _} _ /.
Arguments vec_tail {_ _} _ /.

Arguments vec_O_inv {_ _}.
Arguments vec_S_inv {_ _ _}.

Notation "âŸ¨ âŸ©" := vec_nil.
Infix "âˆ·" := vec_cons.
Notation "v .[ i ]" := (vec_prj v i).

Section vec_map.

  Context {A B: Type} (f : A â†’ B).

  Fixpoint vec_map {n} (v : vec A n) : vec B n :=
    match v with
    | âŸ¨âŸ©    => âŸ¨âŸ©
    | a âˆ· v => f a âˆ· vec_map v
    end.

  Fact vec_prj_map n (v : vec A n) : âˆ€i, (vec_map v).[i] = f v.[i].
  Proof. induction v; intro i; destruct (idx_inv i); cbn; trivial. Qed.

End vec_map.

Notation "âŸ¨ x âŸ©" :=  (x âˆ· âŸ¨âŸ©) (at level 0, format "âŸ¨ x âŸ©").
Notation "âŸ¨ x ; y ; .. ; z âŸ©" :=  (x âˆ· (y âˆ· .. (z âˆ· âŸ¨âŸ©) ..)) (at level 0, format "âŸ¨ x ; y ; .. ; z âŸ©").

Section hvec.

  Variables (X : Type) (P : X â†’ Type).

  Inductive hvec : forall {n}, vec X n â†’ Type :=
    | hvec_nil : hvec âŸ¨âŸ©
    | hvec_cons {n x v} : P x â†’ @hvec n v â†’ hvec (x âˆ· v).

  Definition vec_hmap (f : forall x, P x) : âˆ€ {n} v, @hvec n v :=
    fix loop {n} v :=
      match v with
      | âŸ¨âŸ©    => hvec_nil
      | x âˆ· v => hvec_cons (f x) (loop v)
      end.

End hvec.

Arguments hvec {_} P {_}.
Arguments hvec_nil {_ _}.
Arguments hvec_cons {_ _ _ _ _}.
Arguments vec_hmap {X P} _ {n} _.
